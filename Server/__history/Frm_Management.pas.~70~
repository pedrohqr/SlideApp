unit Frm_Management;

interface

uses
  Winapi.Windows, Winapi.Messages, System.SysUtils, System.Variants, System.Classes, Vcl.Graphics,
  Vcl.Controls, Vcl.Forms, Vcl.Dialogs, Vcl.ToolWin, Vcl.ComCtrls, Vcl.Menus,
  Vcl.StdCtrls, REST.Types, Data.Bind.Components, Data.Bind.ObjectScope,
  REST.Client, Vcl.WinXCtrls, Vcl.Grids, LibDB, frxClass, frxDBSet;

type
  TFManagement = class(TForm)
    Client: TRESTClient;
    Request: TRESTRequest;
    Response: TRESTResponse;
    Memo: TMemo;
    Edt_Song: TEdit;
    lbl_song: TLabel;
    lbl_artist: TLabel;
    Edt_Artist: TEdit;
    Btn_Search: TButton;
    lbl_lyrics: TLabel;
    lbl_status: TLabel;
    Animation: TActivityIndicator;
    btn_register_song: TButton;
    lbl_id_art: TLabel;
    lbl_id_song: TLabel;
    Edt_Audio_Link: TEdit;
    lbl_audio_link: TLabel;
    Edt_SongID: TEdit;
    Label1: TLabel;
    btn_SearchSongID: TButton;
    btn_ShowSlide: TButton;
    frxReport: TfrxReport;
    frxDBDataset: TfrxDBDataset;
    procedure FormClose(Sender: TObject; var Action: TCloseAction);
    procedure Btn_SearchClick(Sender: TObject);
    procedure btn_register_songClick(Sender: TObject);
    procedure FormCreate(Sender: TObject);
    procedure btn_SearchSongIDClick(Sender: TObject);
    procedure Edt_SongIDKeyDown(Sender: TObject; var Key: Word;
      Shift: TShiftState);
    procedure btn_ShowSlideClick(Sender: TObject);
  private
    FConn : TConnection;
    function FormatLineBreak(Text: String): String;
    function SearchArtist(Code : String): Integer;
    function SearchSong(Code: String): Integer;
  public
    { Public declarations }
  end;

var
  FManagement: TFManagement;

implementation

{$R *.dfm}

uses Frm_Main, System.JSON, System.Generics.Collections, FireDAC.Stan.Param,
  FireDAC.Comp.Client, Data.DB;

/// <summary> Format the '\n' to lineBrak of Delphi
/// </summary>
function TFManagement.FormatLineBreak(Text : String): String;
var
  I: Integer;
begin
  for I := 2 to Text.Length-5 do
    if (Text[I]='\') and (Text[I+1]='n') then
    begin
      Result := Result + sLineBreak;
      Delete(Text, I, 1);
    end
    else
      Result := Result + Copy(Text, I, 1);
end;

procedure TFManagement.btn_register_songClick(Sender: TObject);
begin
  try
    with FConn do
    begin
      CreateSP('REG_ARTIST');

      SP.Prepare;
      SP.ParamByName('V_PERSON_ID').AsInteger := SearchArtist(lbl_id_art.Caption);
      SP.ParamByName('V_NAME').AsString       := Edt_Artist.Text;
      SP.ParamByName('V_CODE').AsString       := lbl_id_art.Caption;
      SP.ExecProc;

      CreateSP('REG_SONG');
      SP.Prepare;
      SP.ParamByName('V_SONG_ID').AsInteger   := SearchSong(lbl_id_song.Caption);
      SP.ParamByName('V_NAME').AsString       := Edt_Song.Text;
      SP.ParamByName('V_LYRICS').AsString     := Memo.Text;
      SP.ParamByName('V_AUDIO_LINK').AsString := Trim(Edt_Audio_Link.Text);
      SP.ParamByName('V_ID_ART').AsInteger    := SearchArtist(lbl_id_art.Caption);
      SP.ParamByName('V_CODE').AsString       := lbl_id_song.Caption;

      if Trim(SP.ParamByName('V_AUDIO_LINK').AsString).IsEmpty then
        SP.ParamByName('V_AUDIO_LINK').Clear;

      SP.ExecProc;

      Edt_Song.Text       := EmptyStr;
      Edt_Artist.Text     := EmptyStr;
      Edt_Audio_Link.Text := EmptyStr;
      lbl_id_art.Caption  := EmptyStr;
      lbl_id_song.Caption := EmptyStr;
      Memo.Lines.Clear;

      lbl_status.Caption := 'Status: Cadastrado com sucesso';
    end;
  except on e : exception do
    ShowMessage(e.Message);
  end;
end;

procedure TFManagement.Btn_SearchClick(Sender: TObject);
begin
  Animation.Animate := True;
  TThread.CreateAnonymousThread(procedure
  var
    Return : TJSONObject;
  begin
    try
      Request.Params.AddUrlSegment('song', Edt_Song.Text);
      Request.Params.AddUrlSegment('artist', Edt_Artist.Text);
      Request.Execute;
      if Request.Response.StatusCode = 200 then
      begin
        Return := Response.JSONValue as TJSONObject;
        TThread.Synchronize(nil, procedure
        begin
          Animation.Animate := False;
          Memo.Lines.Clear;

          if Return.GetValue('type').Value = 'exact' then
            lbl_status.Caption := 'Status: Encontrado'
          else
          if Return.GetValue('type').Value = 'aprox' then
            lbl_status.Caption := 'Status: Busca aproximada'
          else
          if Return.GetValue('type').Value = 'song_notfound' then
            lbl_status.Caption := 'Status: Música não encontrada'
          else
          if Return.GetValue('type').Value = 'notfound' then
            lbl_status.Caption := 'Status: Artista não encontrado';
          
          if (Return.GetValue('type').Value = 'exact') or (Return.GetValue('type').Value = 'aprox') then
          begin
            Edt_Song.Text       := ((Return.GetValue('mus') as TJSONArray).Items[0] as TJSONObject).GetValue('name').Value;
            Edt_Artist.Text     := (Return.GetValue('art') as TJSONObject).GetValue('name').Value;
            lbl_id_song.Caption := ((Return.GetValue('mus') as TJSONArray).Items[0] as TJSONObject).GetValue('id').Value;
            lbl_id_art.Caption  := (Return.GetValue('art') as TJSONObject).GetValue('id').Value;
            Memo.Lines.Add(FormatLineBreak(((Return.GetValue('mus') as TJSONArray).Items[0] as TJSONObject).GetValue('text').ToString));
          end;
        end);
      end
      else
      TThread.Synchronize(nil, procedure
      begin
        Animation.Animate := False;
        ShowMessage('Erro: StatusCode '+Request.Response.StatusCode.ToString);
      end);
    except on e : exception do
      TThread.Synchronize(nil, procedure
      begin
        Animation.Animate := False;
        ShowMessage('Erro: '+e.Message);
      end);
    end;
  end).Start;
end;

procedure TFManagement.btn_SearchSongIDClick(Sender: TObject);
var
  I : Integer;
begin
  if (not Trim(Edt_SongID.Text).IsEmpty) and (TryStrToInt(Edt_SongID.Text, I)) then
  with FConn do
  begin
    CreateQuery;
    Query.SQL.Add('SELECT '+
                  '   S.LYRICS, '+
                  '   S.NAME, '+
                  '   P.NAME AS ARTIST, '+
                  '   COALESCE(S.AUDIO_LINK, '''') AS AUDIO_LINK, '+
                  '   COALESCE(S.CODE, '''') AS SONG_CODE, '+
                  '   COALESCE(P.CODE, '''') AS ARTIST_CODE '+
                  'FROM SONG S '+
                  'INNER JOIN PERSON P '+
                  'ON S.ARTIST_ID = P.PERSON_ID '+
                  'WHERE SONG_ID = :pID;');
    Query.ParamByName('pID').AsInteger := StrToInt(Edt_SongID.Text);
    Query.Open;
    if Query.RecordCount > 0 then
    begin
      Edt_Song.Text   := Query.FieldByName('NAME').AsString;
      Edt_Artist.Text := Query.FieldByName('ARTIST').AsString;
      Memo.Lines.Text := Query.FieldByName('LYRICS').AsString;
      lbl_id_art.Caption := Query.FieldByName('ARTIST_CODE').AsString;
      lbl_id_song.Caption := Query.FieldByName('SONG_CODE').AsString;
      Edt_Audio_Link.Text := Query.FieldByName('AUDIO_LINK').AsString;
    end;
  end;
end;

procedure TFManagement.btn_ShowSlideClick(Sender: TObject);
var
  MT : TFDMemTable;
begin
  MT := TFDMemTable.Create(Self);
  try
    //create temp field to dataset of report
    with MT.FieldDefs do
    begin
      with AddFieldDef do
      begin
        Name := 'LYRICS';
        DataType := ftString;
        Size := 10000;
      end;
    end;

    MT.Open;
    MT.Append;
    MT.FieldByName('LYRICS').AsString := Memo.Lines.Text;
    MT.Post;

    frxDBDataset.DataSet := MT;
    frxReport.PrepareReport;
    frxReport.ShowPreparedReport;
  finally
    if Assigned(MT) then
      FreeAndNil(MT);
  end;
end;

procedure TFManagement.Edt_SongIDKeyDown(Sender: TObject; var Key: Word;
  Shift: TShiftState);
begin
  if key = VK_RETURN then
  btn_SearchSongIDClick(Self);
end;

procedure TFManagement.FormClose(Sender: TObject; var Action: TCloseAction);
begin
  if Assigned(FConn) then
    FreeAndNil(FConn);
  FMain.Show;
  Action := TCloseAction.caFree;
  FManagement := nil;
end;

procedure TFManagement.FormCreate(Sender: TObject);
begin
  FConn := TConnection.Create(Self);
end;

/// <summary> Find for Code of the person and returns the ID
/// </summary>
function TFManagement.SearchArtist(Code: String): Integer;
begin
  with FConn do
  begin
    CreateQuery;
    Query.SQL.Add('SELECT PERSON_ID FROM PERSON WHERE CODE LIKE :v_code;');
    Query.ParamByName('v_code').AsString := Code;
    Query.Open;
    if Query.FieldByName('PERSON_ID').IsNull then
      Result := 0
    else
      Result := Query.FieldByName('PERSON_ID').AsInteger;
  end;
end;

/// <summary> Find for Code of the song and returns the ID
/// </summary>
function TFManagement.SearchSong(Code: String): Integer;
begin
  with FConn do
  begin
    CreateQuery;
    Query.SQL.Add('SELECT SONG_ID FROM SONG WHERE CODE LIKE :v_code;');
    Query.ParamByName('v_code').AsString := Code;
    Query.Open;
    if Query.FieldByName('SONG_ID').IsNull then
      Result := 0
    else
      Result := Query.FieldByName('SONG_ID').AsInteger;
  end;
end;


end.
